buildscript {
	dependencies {
		classpath 'org.flywaydb:flyway-mysql:10.0.0'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.flywaydb.flyway' version '10.0.0'
}

group = 'com.USWCicrcleLink'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

def profile = project.findProperty("profile") ?: "local" // default local

flyway {
	if (profile == "prod") {
		url = System.getenv("PROD_DB_URL")
		user = System.getenv("PROD_MYSQL_USER")
		password = System.getenv("PROD_MYSQL_PASS")
		locations = ["classpath:db/migration/common", "classpath:db/migration/prod"]
		cleanDisabled = true
	} else if (profile == "local") {
		url = System.getenv("TEST_DB_URL")
		user = System.getenv("TEST_MYSQL_USER")
		password = System.getenv("TEST_MYSQL_PASS")
		locations = ["classpath:db/migration/common", "classpath:db/migration/local"]
		cleanDisabled = false
	} else {
		throw new GradleException("❌ 정의된 프로필 없음: '${profile}'. Use -Pprofile=test or -Pprofile=prod")
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework:spring-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// lombok
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'

	// db
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.mysql:mysql-connector-j'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// flyway
	// https://mvnrepository.com/artifact/org.flywaydb/flyway-core
	implementation 'org.flywaydb:flyway-core:10.0.0'
	implementation 'org.flywaydb:flyway-mysql:10.0.0'

	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'

	//thymeleaf
	implementation'org.springframework.boot:spring-boot-starter-thymeleaf'

	//jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	//mail
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// excel
	// https://mvnrepository.com/artifact/org.apache.poi/poi
	implementation 'org.apache.poi:poi-ooxml:5.3.0'

	// FCM(푸시 알림)
	// https://mvnrepository.com/artifact/com.google.firebase/firebase-admin
	implementation 'com.google.firebase:firebase-admin:9.3.0'

	// jackson
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

	//db exception
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	//spring security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// aws-s3
	// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-aws
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
	implementation 'com.amazonaws:aws-java-sdk-core:1.12.345'
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.345'

	// XSS 공격 방지 Jsoup
	implementation 'org.jsoup:jsoup:1.16.1'

	//Bucket4j
	implementation 'com.bucket4j:bucket4j-core:8.7.0'
	implementation 'com.bucket4j:bucket4j-redis:8.7.0'
	implementation 'io.lettuce:lettuce-core:6.2.6.RELEASE'
}

tasks.named('test') {
	useJUnitPlatform()
}